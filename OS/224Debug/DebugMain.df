#include "<df>/dragonfruit.h"

#include "<inc>/HALConsole.h"
#include "<inc>/HALLog.h"
#include "<inc>/HALMap.h"
#include "<inc>/HALCrash.h"

#include "<inc>/DebugTrace.h"

#include "../Loader/LoaderGlobal.h"

#include "<inc>/Kernel.h"

extern DebugPrompt { -- }

externptr HALDebugDumpFunction
externptr HALDebuggerFunction

externptr HALResourceKeepHeaders

fn DebugInit { ldrinfo -- }
	if (ldrinfo@ LoaderInfo_Major + @ LOADERMAJOR ~=)
		ldrinfo@ LoaderInfo_Major + @ LOADERMAJOR "LOADERMAJOR mismatch: expected %d, was given %d.\n" "DebugInit" HALLog
		"224Debug can't be used with this system.\n" "DebugInit" HALLog
		return
	end elseif (ldrinfo@ LoaderInfo_Minor + @ LOADERMINOR ~=)
		ldrinfo@ LoaderInfo_Minor + @ LOADERMINOR "LOADERMINOR mismatch: expected %d, was given %d.\n" "DebugInit" HALLog
		"Will proceed, but there may be unexpected behavior.\n" "DebugInit" HALLog
	end

	pointerof DebugDump HALDebugDumpFunction!

	pointerof DebugMain HALDebuggerFunction!

	1 HALResourceKeepHeaders!
end

var DebugCrashLater 0
public DebugCrashLater

fn DebugMain { -- }
	auto oldmode
	CONSOLEMODETTY HALConsoleSetMode oldmode!

	auto ipl
	IPLHIGH KeIPLRaise ipl!

	"224Debug invoked!\n" "DebugMain" HALLog

	DebugPrompt

	oldmode@ HALConsoleSetMode drop

	ipl@ KeIPLLower

	if (DebugCrashLater@)
		0 DebugCrashLater!
		"debug\n" KeCrash
	end
end