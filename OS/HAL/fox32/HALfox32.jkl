//
// Initialization of the fox32 HAL.
//

#INCLUDE "<ll>/Rtl.hjk"
#INCLUDE "../../OSLoader/Headers/Loader.hjk"

#INCLUDE "<inc>/HALDriver.hjk"
#INCLUDE "<inc>/HALCPU.hjk"
#INCLUDE "<inc>/HALMap.hjk"
#INCLUDE "<inc>/HALCrash.hjk"
#INCLUDE "<inc>/HALLog.hjk"
#INCLUDE "<inc>/HALfox32IO.hjk"
#INCLUDE "<inc>/HALIPL.hjk"
#INCLUDE "<inc>/HALNvram.hjk"

#INCLUDE "<ll>/OSDLL/OS.hjk"

EXTERN FN HALfox32InterruptInit ()

PUBLIC KeThreadCurrentStackTop : ^VOID

EXTERN HALLoaderInfo : ^BlInfoRecord

PUBLIC HALPlatformKernelPageDirectory : ULONG

PUBLIC HALPlatformModel : UBYTE[32]

PUBLIC HALCPUModel : UBYTE[32]

PUBLIC HALBusModel : UBYTE[32]

FN HALPlatformInfo ()

    // we're supposed to print platform info in this function

    RtlPrint (
        "Fox32 (%dMB)",
        (HALLoaderInfo^.TotalPages << RTL_PAGE_SHIFT) / 1048576,
    )
END

#ENTERSECTION "INITtext"
FN HALPlatformInit (
    IN ldrinfo : ^BlInfoRecord,
)

    RtlCopyString (
        &HALPlatformModel[0], // dest
        "Fox32", // src
        -1, // max
    )

    RtlCopyString (
        &HALCPUModel[0], // dest
        "fox32", // src
        -1, // max
    )

    RtlCopyString (
        &HALBusModel[0], // dest
        "foxbus", // src
        -1, // max
    )

    HALLog ( "HALMain", "Platform: " )

    HALPlatformInfo ()
END
#LEAVESECTION

FN HALPlatformCrash ()
    NOTHING
END

FN HALNvramQuery (
    IN query : ^OSNvramQuery,
) : UWORD

    RETURN STATUS_NOT_SUPPORTED
END

FN HALNvramSet (
    IN delete : UWORD,
    IN query : ^OSNvramQuery,
) : UWORD

    RETURN STATUS_NOT_SUPPORTED
END

FN HALNvramRead (
    IN query : ^OSNvramQuery,
    IN index : UWORD,
    OUT nextindex : UWORD,
) : UWORD

    // query a variable by opaque index and return the next index value.
    // if -1 is provided, then the first variable will be read.

    RETURN STATUS_NOT_SUPPORTED
END

#ASM [

// value port --
HALfox32Out:
.global HALfox32Out
    out a1, a0
    ret

// port -- value
HALfox32In:
.global HALfox32In
    in a3, a0
    ret

]

#DEFINE FOX32_POWER_PORT 0x80010000

EXTERN FN HALfox32Reset ()

FN HALPlatformExit (
    IN ret : UWORD,
    IN mode : UWORD,
)

    // MUST BE CALLED WITH INTERRUPTS DISABLED

    IF mode == OSSHUTDOWN_HALT THEN
        HALfox32Out ( 0, FOX32_POWER_PORT )
        WHILE TRUE DO END
    ELSEIF mode == OSSHUTDOWN_REBOOT THEN
        HALfox32Reset ()
    ELSE
        RtlPrint ( "?" )

        HALfox32Reset ()
    END
END

FN HALPlatformIsComputerOnFire (
    OUT isonfire : UWORD,
) : UWORD

    // all fox32 CPU installations have a highly trained team of foxes on call
    // at all times, who are skilled enough to absolutely prevent "mistakes"
    // before they can result in a conflagration.

    isonfire = 0
    RETURN 0
END