//
// Implements the platform-dependent part of interrupt support
//

#INCLUDE "<ll>/Rtl.hjk"
#INCLUDE "../../OSLoader/Headers/Loader.hjk"

#INCLUDE "<inc>/HALLog.hjk"

#INCLUDE "<inc>/HALMap.hjk"
#INCLUDE "<inc>/HALIPL.hjk"
#INCLUDE "<inc>/HALInterrupt.hjk"
#INCLUDE "<inc>/HALCPU.hjk"
#INCLUDE "<inc>/HALCrash.hjk"

#INCLUDE "<ll>/OSDLL/OS.hjk"

PUBLIC KeIPLCurrent := IPL_DPC

PUBLIC HALPlatformInterruptHandlers : ^VOID[256]

FN (HALInterruptHandler) HALfox32InterruptSpurious (
    IN trapframe : ^OSContext,
    IN int : UWORD,
)

    HALCrash ( "spurious interrupt: 0x%x\n", int )
END

FN HALfox32InterruptInit ()

    i := 0

    WHILE i < 256 DO
        HALPlatformInterruptHandlers[i] = &HALfox32InterruptSpurious

        i += 1
    END
END

FN HALPlatformInterruptRegister (
    IN handler : HALInterruptHandler,
    IN int : UWORD,
    IN ipl : UWORD,
)

    IF HALPlatformInterruptHandlers[int] != &HALfox32InterruptSpurious THEN
        HALCrash ( "HALPlatformInterruptRegister: attempt to register IRQ #%d twice\n", int )
    END

    HALPlatformInterruptHandlers[int] = handler
END

FN HALPlatformInterruptUnregister (
    IN handler : HALInterruptHandler,
    IN int : UWORD,
    IN ipl : UWORD,
)

    IF HALPlatformInterruptHandlers[int] == &HALfox32InterruptSpurious THEN
        HALCrash ( "HALPlatformInterruptUnregister: attempt to unregister IRQ #%d; wasn't registered\n", int )
    END

    HALPlatformInterruptHandlers[int] = &HALfox32InterruptSpurious
END