#INCLUDE "<ll>/Rtl.hjk"
#INCLUDE "<inc>/HALCrash.hjk"

#INCLUDE "<inc>/HALCPU.hjk"
#INCLUDE "<inc>/HALMap.hjk"
#INCLUDE "<inc>/HALInterrupt.hjk"
#INCLUDE "<inc>/HALIPL.hjk"

#INCLUDE "<inc>/HALDebug.hjk"

#INCLUDE "<ll>/OSDLL/OS.hjk"

// was once HALXr17032Exception.df until that stuff was moved into the kernel

EXTERN KeThreadCurrentStackTop : ^VOID

EXTERN KeIPLCurrent : UWORD

PUBLIC HALfox32InterruptsDisabled := 1

#ASM [

// -- sp
HALCPUGetSP:
.global HALCPUGetSP
    mov  a3, sp
    ret

// -- rs
HALCPUInterruptDisable:
.global HALCPUInterruptDisable
    icl

    mov  a3, [HALfox32InterruptsDisabled]
    mov  [HALfox32InterruptsDisabled], 1

    ret

// rs --
HALCPUInterruptRestore:
.global HALCPUInterruptRestore
    icl

    mov  [HALfox32InterruptsDisabled], a0

    cmp  a0, 1
    ifz  jmp .dontenable
    cmp  [KeIPLCurrent], IPL_DPC
    ifgt jmp .dontenable

    ise

.dontenable:
    ret

// --
HALCPUInterruptEnable:
.global HALCPUInterruptEnable
    icl

    mov  [HALfox32InterruptsDisabled], 0

    cmp  [KeIPLCurrent], IPL_DPC
    ifgt jmp .dontenable

    ise

.dontenable:
    ret

// old new -- 
HALCPUContextSwitch:
.global HALCPUContextSwitch
    mov  esp, [KeThreadCurrentStackTop]

    sub  sp, 5 //skip eflagsB and badaddr
    push fp
    push r31
    push tp
    push at
    push s17
    push s16
    push s15
    push s14
    push s13
    push s12
    push s11
    push s10
    push s9
    push s8
    push s7
    push s6
    push s5
    push s4
    push s3
    push s2
    push s1
    push s0
    sub  sp, 44

    mov  [a0], sp
    mov  sp, a1

    add  sp, 44
    pop  s0
    pop  s1
    pop  s2
    pop  s3
    pop  s4
    pop  s5
    pop  s6
    pop  s7
    pop  s8
    pop  s9
    pop  s10
    pop  s11
    pop  s12
    pop  s13
    pop  s14
    pop  s15
    pop  s16
    pop  s17
    pop  at
    pop  tp
    pop  r31
    pop  fp
    add  sp, 5 //skip eflagsB and badaddr

    ret

// pgtb --
HALfox32MapSwitch:
.global HALfox32MapSwitch
    tlb  a0
    ret

// vpn --
HALfox32TLBFlush:
.global HALfox32TLBFlush
    sla  a0, 12
    flp  a0
    ret

HALDcacheExpunge:
.global HALDcacheExpunge
    ret

HALIcacheSynchronize:
.global HALIcacheSynchronize
    ret

HALCPUFence:
.global HALCPUFence
    ret

]