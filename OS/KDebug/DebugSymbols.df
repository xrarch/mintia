//
// Implements bootloader symbol table traversal.
//

#include "<df>/dragonfruit.h"

#include "<inc>/HALConsole.h"
#include "<inc>/HALLog.h"

#include "../Loader/LoaderGlobal.h"

externptr HALLoaderInfo

fn DebugGetSymbol { dll name -- symbol }
	auto ptr
	dll@ DLL_LOFF + LOFFLoaded_SymbolTable + @ ptr!

	auto i
	0 i!

	auto count
	dll@ DLL_LOFF + LOFFLoaded_SymbolCount + @ count!

	auto stab
	dll@ DLL_LOFF + LOFFLoaded_StringTable + @ stab!

	0 symbol!

	while (i@ count@ <)
		if (ptr@ LOFFSymbol_Type + @ 1 ==)
			auto sname
			ptr@ LOFFSymbol_NameOffset + @ stab@ + sname!

			if (sname@ name@ strcmp)
				ptr@ symbol!
				return
			end
		end

		LOFFSymbol_SIZEOF ptr +=
		1 i +=
	end
end

fn DebugGetSymbolAddress { dll name -- address }
	auto sym
	dll@ name@ DebugGetSymbol sym!

	0 address!

	if (sym@)
		auto section
		dll@ DLL_LOFF +
		LOFFLoaded_TextLinkedAddr +
		sym@ LOFFSymbol_Section + @ 1 -
		LOFFLoadedSection_SIZEOF * + section!

		sym@ LOFFSymbol_Value + @ section@ LOFFLoadedSection_RealAddr + @ + address!
	end
end

fn DebugGetSymDLL { pc -- dll symbase symname }
	HALLoaderInfo@ LoaderInfo_DLLListHead + @ dll!

	0 symbase!
	0 symname!

	while (dll@)
		auto loff
		dll@ DLL_LOFF + loff!

		if (pc@ loff@ LOFFLoaded_TextRealAddr + @ <)
			dll@ DLL_Next + @ dll!
			continue
		end

		if (pc@ loff@ LOFFLoaded_TextRealAddr + @ loff@ LOFFLoaded_TextSize + @ + >=)
			dll@ DLL_Next + @ dll!
			continue
		end

		auto ptr
		loff@ LOFFLoaded_SymbolTable + @ ptr!

		auto i
		0 i!

		auto count
		loff@ LOFFLoaded_SymbolCount + @ count!

		auto stab
		loff@ LOFFLoaded_StringTable + @ stab!

		while (i@ count@ <)
			if (ptr@ LOFFSymbol_Section + @ 1 == ptr@ LOFFSymbol_Type + @ 1 == &&)
				auto sptr
				ptr@ LOFFSymbol_Value + @ loff@ LOFFLoaded_TextRealAddr + @ + sptr!

				if (pc@ sptr@ >=)
					sptr@ symbase!
					if (ptr@ LOFFSymbol_NameOffset + @ 0xFFFFFFFF ~=)
						ptr@ LOFFSymbol_NameOffset + @ stab@ + symname!

						// symname@ sptr@ "%x %s " Printf
					end else
						0 symname!
					end
				end elseif (pc@ sptr@ <)
					return
				end
			end

			LOFFSymbol_SIZEOF ptr +=
			1 i +=
		end

		if (symbase@)
			return
		end

		dll@ DLL_Next + @ dll!
	end

	0 symbase!
	0 symname!
end

fn DebugGetDLL { name -- dll }
	HALLoaderInfo@ LoaderInfo_DLLListHead + @ dll!

	while (dll@)
		if (dll@ DLL_Name + name@ strcmp)
			return
		end

		dll@ DLL_Next + @ dll!
	end
end