//
// Implements a very naive bootloader memory manager, and initialization of
// virtual regions expected by the MINTIA kernel.
//

#include "<df>/dragonfruit.h"
#include "Loader.h"

buffer MemBitmap INITBITMAPSZ
public MemBitmap

var MemAllocated 0
public MemAllocated

var MemPages 0
public MemPages

var MemPageFrameDB 0
public MemPageFrameDB

var MemPageDirectory 0
public MemPageDirectory

var MemHeap 0
public MemHeap

var MemHeapSize 0
public MemHeapSize

const HEAPMAXDEFAULT (2048 1024 *)

const HEAPMINDEFAULT (32 1024 *)

const HEAPSHFDEFAULT 5

extern MemEnablePaging { -- }

fn MemInit { -- }
	auto bm
	MemBitmap bm!

	auto pages
	LoaderTotalRAM@ PAGESHIFT >> pages!

	// limit to first 256MB as thats all we have bitmap space for.
	// (LIMNstation only supports this much RAM anyway)
	pages@ 65536 min MemPages!

	// reserve loader code and data

	// XXX this only works because a3x firmware and bootloader data are
	// contiguous and start at 0x0

	auto ldrtop
	pointerof _bss_end ldrtop!

	ldrtop@ PAGEOFFSETMASK + PAGESHIFT >> // runlength
	0 // index
	MemSetBits

	auto astart

	auto ok

	1 0 MemAlloc ok! astart!

	if (ok@)
		"couldn't allocate kernel page directory\n" LoaderPanic
	end

	astart@ PAGESHIFT << MemPageDirectory!

	MemPageDirectory@
	PAGESIZE
	0
	memset

	// identity map physical memory at 0x00000000 and IDENTITYSPACE

	auto kspc
	IDENTITYSPACE kspc!

	0 astart!

	while (astart@ LoaderTotalRAM@ <)
		astart@ astart@ 0 MemMapPage ok!

		if (ok@)
			"couldn't identity map memory\n" LoaderPanic
		end

		astart@ kspc@ 0 MemMapPage ok!

		if (ok@)
			"couldn't identity map kernel space\n" LoaderPanic
		end

		PAGESIZE kspc +=
		PAGESIZE astart +=
	end

	// allocate the pool space page tables so it gets properly
	// mapped into userspace processes.

	// XXX could probably be done in MmInitPhase0 now

	// we do 8 because each page table maps 4MB and 32/4MB = 8.

	auto pdi
	POOLSPACE 22 >> 2 << MemPageDirectory@ + pdi!

	auto i
	0 i!

	while (i@ 8 <)
		0 // pstart
		MemAllocSinglePage ok! astart!

		if (ok@)
			"couldn't allocate page table for pool space\n" LoaderPanic
		end

		astart@ 5 << PTE_V | pdi@!

		astart@ PAGESHIFT << // ptr
		PAGESIZE // size
		0 // word
		memset

		4 pdi +=
		1 i +=
	end

	MemEnablePaging

	// allocate these before we fragment physical memory by loading OS modules

	auto rps

	// allocate pageframe DB

	pages@ 64 * rps!

	rps@ "pageframe db is %d bytes" Printf

	rps@ PAGEOFFSETMASK + PAGESHIFT >> PFDBBASE PAGESHIFT >> 0 MemAllocUpper ok! astart!

	if (ok@)
		"couldn't allocate pageframe db\n" LoaderPanic
	end

	astart@ PAGESHIFT << MemPageFrameDB!

	MemPageFrameDB@ " at 0x%08x\n" Printf

	// allocate kernel heap

	auto heapmax
	"HEAPMAX" ArgsValue heapmax!

	if (heapmax@)
		heapmax@ atoi 1024 * heapmax!
	end else
		HEAPMAXDEFAULT heapmax!
	end

	auto heapmin
	"HEAPMIN" ArgsValue heapmin!

	if (heapmin@)
		heapmin@ atoi 1024 * heapmin!
	end else
		HEAPMINDEFAULT heapmin!
	end

	auto heapshf
	"HEAPSHF" ArgsValue heapshf!

	if (heapshf@)
		heapshf@ atoi heapshf!
	end else
		HEAPSHFDEFAULT heapshf!
	end

	pages@ heapshf@ >> PAGESHIFT << rps!

	if (rps@ heapmin@ <)
		heapmin@ rps!
	end elseif (rps@ heapmax@ >)
		heapmax@ rps!
	end

	// round down to nearest power of two
	// I dont know why this works. I found it on a website somewhere

	rps@ 1 >> rps |=
	rps@ 2 >> rps |=
	rps@ 4 >> rps |=
	rps@ 8 >> rps |=
	rps@ 16 >> rps |=
	rps@ 1 >> rps -=

	rps@ PAGEOFFSETMASK + PAGESHIFT >> rps!

	// rps@ PAGESHIFT << "heap is %d bytes" Printf

	// rps@ HEAPBASE PAGESHIFT >> 0 MemAllocUpper ok! astart!

	// if (ok@)
	// 	"\ncouldn't allocate kernel heap\n" LoaderPanic
	// end

	// astart@ PAGESHIFT << MemHeap!
	// rps@ PAGESHIFT << MemHeapSize!

	// MemHeap@ " at 0x%08x\n" Printf
end

fn HexDump { addr len -- }
	auto i
	0 i!

	while (len@)
		if (i@ 4 % 0 == i@ &&)
			'\n' Putc
		end

		addr@ @ "%9x " Printf

		1 i +=
		4 addr +=
		4 len -=
	end

	'\n' Putc
end

fn MemSetBits { runlength index -- }
	// taken from ExBitmapSetBits

	auto bmp
	MemBitmap bmp!

	if (runlength@ 0 ==)
		return
	end

	auto bitoffset
	index@ 31 & bitoffset!

	auto curlongptr
	bmp@ index@ 5 >> 2 << + curlongptr!

	if (bitoffset@ runlength@ + 32 <=)
		// you are expected to understand this

		curlongptr@@ 0xFFFFFFFF 32 runlength@ - >> bitoffset@ << | curlongptr@! 

		return
	end

	curlongptr@@ 0xFFFFFFFF bitoffset@ << | curlongptr@!

	4 curlongptr +=
	32 bitoffset@ - runlength -=

	while (runlength@ 32 >=)
		0xFFFFFFFF curlongptr@!

		4 curlongptr +=
		32 runlength -=
	end

	if (runlength@)
		curlongptr@@ 0xFFFFFFFF runlength@ << ~ | curlongptr@!
	end
end

fn MemClearBits { runlength index -- }
	auto bmp
	MemBitmap bmp!

	// taken from ExBitmapClearBits

	if (runlength@ 0 ==)
		return
	end

	auto bitoffset
	index@ 31 & bitoffset!

	auto curlongptr
	bmp@ index@ 5 >> 2 << + curlongptr!

	if (bitoffset@ runlength@ + 32 <=)
		// you are expected to understand this

		curlongptr@@ 0xFFFFFFFF 32 runlength@ - >> bitoffset@ << ~ & curlongptr@! 

		return
	end

	curlongptr@@ 0xFFFFFFFF bitoffset@ << ~ & curlongptr@!

	4 curlongptr +=
	32 bitoffset@ - runlength -=

	while (runlength@ 32 >=)
		0 curlongptr@!

		4 curlongptr +=
		32 runlength -=
	end

	if (runlength@)
		curlongptr@@ 0xFFFFFFFF runlength@ << & curlongptr@!
	end
end

const PTE_V  1
const PTE_W  2
const PTE_K  4
const PTE_NC 8
const PTE_G  16

const PTE_KPAGE (PTE_V PTE_W | PTE_K |)

fn MemMapPage { phys virt nc -- ok }
	0 ok!

	auto pdi
	virt@ 22 >> 2 << MemPageDirectory@ + pdi!

	auto pt
	pdi@@ 5 >> PAGESHIFT << pt!

	if (pt@ 0 ==)
		1 0 MemAlloc ok! pt!

		if (ok@)
			return
		end

		PAGESHIFT pt <<=

		pt@ PAGESIZE 0 memset

		pt@ PAGESHIFT >> 5 << PTE_V | pdi@!
	end

	virt@ PAGESHIFT >> 1023 & 2 << pt@ + pdi!

	if (pdi@@ PTE_V &)
		virt@ PAGESHIFT >> "MemMapPage: vpn %x is already mapped!\n" LoaderPanic
	end

	auto flags
	PTE_KPAGE flags!

	if (virt@ KERNELSPACE &)
		PTE_G flags |=
	end

	if (nc@)
		PTE_NC flags |=
	end

	phys@ PAGESHIFT >> 5 << flags@ | pdi@!
end

const PAGEMAX (POOLSPACE PAGESHIFT >>)

fn MemFindEmptyRow { length start must -- ok ava }
	if (start@ KERNELSPACE <)
		"MemFindEmptyRow: start was below kernelspace\n" LoaderPanic
	end

	auto i
	start@ PAGESHIFT >> i!

	auto pd
	MemPageDirectory@ start@ 22 >> 2 << + pd!

	auto pt
	pd@@ 5 >> PAGESHIFT << pt!

	auto max
	PAGEMAX max!

	auto r
	0 r!

	auto row
	0 row!

	auto cv
	start@ cv!
	start@ ava!

	auto rmnd
	i@ 1023 & rmnd!

	if (rmnd@)
		if (pt@ 0 ==)
			1024 rmnd@ - row!
			1024 rmnd@ - i +=
			0x400000 rmnd@ PAGESHIFT << - cv +=
		end else
			rmnd@ 2 << pt +=
		end

		4 pd +=
	end

	while (1)
		if (row@ length@ >=)
			0 ok!
			return
		end

		if (i@ max@ >=)
			if (start@ KERNELSPACE ==)
				(* already searched the whole thing *)
				break
			end

			if (r@)
				break
			end

			1 r!
			KERNELSPACE PAGESHIFT >> i!
			0 row!
			start@ PAGESHIFT >> max!
			KERNELSPACE ava!
			KERNELSPACE cv!
			MemPageDirectory@ KERNELSPACE 22 >> 2 << + pd!
		end

		if (i@ 1023 & 0 ==)
			pd@@ 5 >> PAGESHIFT << pt!

			4 pd +=
		end

		if (pt@ 0 ==)
			1024 row +=
			1024 i +=
			0x400000 cv +=

			continue
		end

		if (pt@@)
			if (must@)
				break
			end

			0 row!
			cv@ PAGESIZE + ava!
		end else
			1 row +=
		end
		
		1 i +=
		4 pt +=
		PAGESIZE cv +=
	end

	ERR ok!
end

fn MemMapIORange { pages phys -- vaddr ok }
	pages@ KERNELSTRUCTURES 0 MemFindEmptyRow vaddr! ok!

	if (ok@)
		return
	end

	auto i
	0 i!

	auto vptr
	vaddr@ vptr!

	while (i@ pages@ <)
		phys@ vptr@ 1 MemMapPage ok!

		if (ok@)
			return
		end

		PAGESIZE vptr +=
		PAGESIZE phys +=
		1 i +=
	end
end

fn MemAllocUpper { pages vstart must -- astart ok }
	pages@ vstart@ PAGESHIFT << must@ MemFindEmptyRow astart! ok!

	if (ok@)
		-1 astart!

		return
	end

	// astart@ ok@ "ok=%08x astart=%08x\n" Printf

	auto pptr

	auto vptr
	astart@ vptr!

	auto i
	0 i!

	while (i@ pages@ <)
		1 0 MemAlloc ok! pptr!

		if (ok@)
			// XXX doesn't free everything it just allocated...
			return
		end

		pptr@ PAGESHIFT << vptr@ 0 MemMapPage ok!

		if (ok@)
			return
		end

		PAGESIZE vptr +=
		1 i +=
	end 

	PAGESHIFT astart >>=

	0 ok!
end

fn MemFree { pages pstart -- }
	pages@ MemAllocated -=

	pages@ // runlength
	pstart@ // index
	MemClearBits
end

var MemLastAlloc 0

fn MemAllocSinglePage { pstart -- astart ok }
	if (pstart@ 0 ==)
		MemLastAlloc@ pstart!
	end

	auto chend
	MemPages@ chend!

	if (pstart@ chend@ >=)
		0 pstart!
	end

	auto i
	pstart@ i!

	auto bmp
	MemBitmap i@ 32 / 4 * + bmp!

	auto mode
	0 mode!

	while (bmp@@ 0xFFFFFFFF ==)
		4 bmp +=
		32 i +=

		if (i@ chend@ >=)
			if (mode@)
				-1 ok!
				return
			end else
				0 i!
				1 mode!
				pstart@ chend!
				MemBitmap bmp!
			end
		end
	end

	auto word
	bmp@@ word!

	auto j
	0 j!

	while (j@ 32 <)
		if (word@ j@ bitget ~~)
			word@ j@ bitset bmp@!

			i@ 31 ~ & j@ | astart!
			astart@ 1 + MemLastAlloc!

			0 ok!
			return
		end

		1 j +=
	end

	"MemAllocSinglePage\n" LoaderPanic
end

fn MemAlloc { pages pstart -- astart ok }
	-1 ok!

	if (pages@ 0 ==)
		"tried to allocate 0 pages\n" LoaderPanic
	end

	if (pages@ 1 ==)
		pstart@ MemAllocSinglePage ok! astart!
		return
	end

	auto chend
	MemPages@ chend!

	auto i
	pstart@ i!

	auto bmp
	MemBitmap i@ 32 / 4 * + bmp!

	auto run
	0 run!

	auto mode
	0 mode!

	while (1)
		if (i@ chend@ >=)
			if (mode@)
				-1 ok!
				return
			end else
				0 i!
				1 mode!
				0 run!
				pstart@ chend!
				MemBitmap bmp!
			end
		end

		if (bmp@@ i@ 31 & bitget)
			0 run!
		end else
			if (run@ 0 ==)
				i@ astart!
			end

			1 run +=
		end

		if (run@ pages@ >=)
			pages@ // runlength
			astart@ // index
			MemSetBits

			pages@ MemAllocated +=

			0 ok!
			return
		end 

		1 i +=

		if (i@ 31 & ~~)
			4 bmp +=

			if (bmp@@ 0xFFFFFFFF ==)
				32 i +=
				0 run!
				4 bmp +=
			end elseif (bmp@@ 0x00000000 ==)
				if (run@ 0 ==)
					i@ astart!
				end

				32 i +=
				32 run +=
				4 bmp +=
			end
		end
	end
end

fn MemDumpBitmap { -- }
	auto bmp
	MemBitmap bmp!

	auto i
	0 i!

	auto ende
	MemPages@ 1024 min ende!

	while (i@ ende@ <)
		auto bit
		i@ 7 & bit!

		if (bit@ 0 ==)
			'|' Putc
		end

		bmp@ i@ 8 / + gb bit@ bitget bit!

		if (bit@)
			'x' Putc
		end else
			' ' Putc
		end

		1 i +=
	end
end

const RS_MMU 4

asm "

MemExit:
.global MemExit
	mtcr rs, zero
	ret

MemEnablePaging:
.global MemEnablePaging
	subi t0, zero, 1
	ftlb t0, t0

	mtcr asid, zero

	la   t0, MemPageDirectory
	mov  t0, long [t0]
	mtcr pgtb, t0
	mfcr t0, rs
	ori  t0, t0, RS_MMU
	mtcr rs, t0
	ret

"