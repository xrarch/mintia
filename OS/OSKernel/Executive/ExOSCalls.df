#include "<df>/dragonfruit.h"

#include "<inc>/HALLog.h"
#include "<inc>/HALRTC.h"
#include "<inc>/HALCPU.h"
#include "<inc>/HALDebug.h"
#include "<inc>/HALDriver.h"
#include "<inc>/HALConsole.h"

#include "<inc>/Kernel.h"

#include "<inc>/Executive.h"

#include "<inc>/Object.h"

#include "<inc>/Memory.h"

#include "<inc>/Security.h"

#include "<inc>/Process.h"

#include "<inc>/IO.h"

#include "<ll>/OSDLL/OS.h"

fn ExCallGetString { max string -- buf ok }
	max@ // bytes
	'cStr' // tag
	MmAllocWithTag ok! buf!

	if (ok@)
		return
	end

	buf@ // dest
	string@ // src
	max@ 1 - // max
	KeSafeStringCopyIn ok!

	if (ok@)
		buf@ MmFree
	end
end

fn ExCallFreeString { buf -- }
	buf@ MmFree
end

fn OSConsolePutCharacter { c -- ok }
	SeAmIAdmin ok!

	if (ok@)
		// process must be owned by an admin to print to HAL console
		return
	end

	c@ HALPutc
end

fn OSSystemAbort { num1 num2 msg -- ok }
	SeAmIAdmin ok!

	if (ok@)
		// process must be owned by an admin to abort the system
		return
	end

	auto msgbuf
	128 alloc msgbuf!

	msgbuf@ // dest
	msg@ // src
	127 // max
	strncpy

	num2@ num1@ msgbuf@ "OSSystemAbort: %s %08x %08x\n" KeCrash
end