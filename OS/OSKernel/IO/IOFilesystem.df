//
// Implements filesystem volume management.
//

#include "<df>/dragonfruit.h"

#include "<inc>/HALLog.h"
#include "<inc>/HALRTC.h"
#include "<inc>/HALCPU.h"
#include "<inc>/HALDebug.h"
#include "<inc>/HALDriver.h"
#include "<inc>/HALMap.h"

#include "<inc>/Kernel.h"

#include "<inc>/Executive.h"

#include "<inc>/Security.h"

#include "<inc>/Memory.h"

#include "<inc>/Object.h"

#include "<inc>/Process.h"

#include "<inc>/IO.h"

#include "<ll>/OSDLL/OS.h"

var IOFilesystemListHead 0
public IOFilesystemListHead

var IOMountListHead 0
public IOMountListHead

var IOMountCount 0
public IOMountCount

fn IOFilesystemRegister { filesystem -- ok }
	fnsection "INIT$text"

	// assumes being run single-threaded (i.e. in a DriverInit routine)...

	0 ok!

	IOFilesystemListHead@ filesystem@ IOFilesystem_Next + !
	filesystem@ IOFilesystemListHead!
end

fn IOMountListLock { -- ok }
	fnsection "PAGE$text"

	KERNELMODE // waitmode
	1 // alertable
	OSWAIT_TIMEOUTINFINITE // timeout
	IOMountListMutex // object
	KeThreadWaitForObject ok!
end

fn IOMountListLockUnalertable { -- }
	fnsection "PAGE$text"

	auto ok

	KERNELMODE // waitmode
	0 // alertable
	OSWAIT_TIMEOUTINFINITE // timeout
	IOMountListMutex // object
	KeThreadWaitForObject ok!

	if (DEBUGCHECKS)
		if (ok@)
			ok@ "IOMountListLockUnalertable: failed to lock mount list (%i)\n" KeCrash
		end
	end
end

fn IOMountListUnlock { -- }
	fnsection "PAGE$text"

	0 // abandon
	IOMountListMutex // mutex
	KeMutexRelease drop
end

fn IOFilesystemMount { flags handle fsname -- mount ok }
	fnsection "PAGE$text"

	auto fileobject

	auto access

	IOFileTypeObject@ // type
	handle@ // handle
	ObObjectReferenceByHandle ok! fileobject! access!

	if (ok@)
		return
	end

	access@ // access
	ACCESS_READ // permission
	SeCheckAccess ok!

	if (ok@)
		fileobject@ ObObjectDereferenceByPointer drop

		return
	end

	if (flags@ OSMOUNT_READONLY & ~~)
		access@ // access
		ACCESS_WRITE // permission
		SeCheckAccess ok!

		if (ok@)
			fileobject@ ObObjectDereferenceByPointer drop

			return
		end
	end

	flags@ // flags
	fileobject@ // fileobject
	fsname@ // fsname
	IOMountObject ok! mount!

	fileobject@ ObObjectDereferenceByPointer drop
end

fn IOMountObject { flags fileobject fsname -- mount ok }
	auto fcb
	fileobject@ IOFile_FileControlBlock + @ fcb!

	auto filetype
	fcb@ IOFileControlBlock_FileType + @ filetype!

	if (filetype@ OSFILETYPE_CHARDEVICE ==)
		STATUS_NOT_SUPPORTED ok!

		return
	end elseif (filetype@ OSFILETYPE_DIRECTORY ==)
		STATUS_IS_A_DIRECTORY ok!

		return
	end elseif (filetype@ OSFILETYPE_FILE ==)
		STATUS_NOT_SUPPORTED ok!

		return
	end

	auto filesystem
	0 filesystem!

	if (fsname@)
		IOFilesystemListHead@ filesystem!

		while (filesystem@)
			if (filesystem@ IOFilesystem_Name + @ fsname@ strcmp)
				break
			end

			filesystem@ IOFilesystem_Next + @ filesystem!
		end

		if (filesystem@ ~~)
			STATUS_NO_SUCH_FILESYSTEM ok!

			return
		end
	end

	auto rs
	HALCPUInterruptDisable rs!

	if (fcb@ IOFileControlBlock_Mount + @)
		rs@ HALCPUInterruptRestore

		STATUS_DEVICE_BUSY ok!

		return
	end elseif (fcb@ IOFileControlBlock_Flags + @ IOFCBFLAG_MOUNTBUSY &)
		rs@ HALCPUInterruptRestore

		STATUS_TRY_AGAIN_LATER ok!

		return
	end elseif (fcb@ IOFileControlBlock_ParseCount + @)
		rs@ HALCPUInterruptRestore

		STATUS_TRY_AGAIN_LATER ok!

		return
	end

	IOFCBFLAG_MOUNTBUSY fcb@ IOFileControlBlock_Flags + |=

	rs@ HALCPUInterruptRestore

	IOMount_SIZEOF // bytes
	'IOMt' // tag
	CANBLOCK // flags
	MmAllocWithTag ok! mount!

	if (ok@)
		HALCPUInterruptDisable rs!
		IOFCBFLAG_MOUNTBUSY ~ fcb@ IOFileControlBlock_Flags + &=
		rs@ HALCPUInterruptRestore

		return
	end

	0 mount@ IOMount_Next + !
	0 mount@ IOMount_Prev + !

	0 mount@ IOMount_FSContext + !
	fileobject@ mount@ IOMount_VolumeFile + !
	0 mount@ IOMount_RootFCB + !
	flags@ mount@ IOMount_Flags + !
	0 mount@ IOMount_RealVolumeFile + !
	0 mount@ IOMount_VolumeCapturedFCB + !

	STATUS_BAD_FILESYSTEM ok!

	auto mountfunc

	if (filesystem@ ~~)
		// no filesystem was explicitly given.
		// try any

		IOFilesystemListHead@ filesystem!

		while (filesystem@)
			filesystem@ mount@ IOMount_Filesystem + !
			
			if (filesystem@ IOFilesystem_Flags + @ IOFSFLAG_NOAUTO & ~~)
				filesystem@ IOFilesystem_MountFunction + @ mountfunc!

				if (DEBUGCHECKS)
					if (mountfunc@ ~~)
						"IOMountObject: filesystem had no mount function 1\n" KeCrash
					end
				end

				mount@ // mount
				mountfunc@ IOFilesystemMountFunction ok!

				if (ok@ 0 ==)
					// successfully mounted as this filesystem

					break
				end
			end

			filesystem@ IOFilesystem_Next + @ filesystem!
		end
	end else
		filesystem@ mount@ IOMount_Filesystem + !

		filesystem@ IOFilesystem_MountFunction + @ mountfunc!

		if (DEBUGCHECKS)
			if (mountfunc@ ~~)
				"IOMountObject: filesystem had no mount function 2\n" KeCrash
			end
		end

		mount@ // mount
		mountfunc@ IOFilesystemMountFunction ok!
	end

	if (ok@)
		// did not successfully mount

		mount@ MmFree

		HALCPUInterruptDisable rs!
		IOFCBFLAG_MOUNTBUSY ~ fcb@ IOFileControlBlock_Flags + &=
		rs@ HALCPUInterruptRestore

		return
	end

	mount@ fcb@ IOFileControlBlock_Mount + !

	// bias the reference count of the volume file object
	fileobject@ ObObjectReferenceByPointer drop

	IOMountListLockUnalertable

	// add to head of mount list

	auto h
	IOMountListHead@ h!

	if (h@)
		mount@ h@ IOMount_Prev + !
	end

	h@ mount@ IOMount_Next + !
	mount@ IOMountListHead!

	1 IOMountCount +=

	IOMountListUnlock

	HALCPUInterruptDisable rs!
	IOFCBFLAG_MOUNTBUSY ~ fcb@ IOFileControlBlock_Flags + &=
	rs@ HALCPUInterruptRestore
end

fn IOFilesystemUnmount { handle -- ok }
	fnsection "PAGE$text"

	auto fileobject

	auto access

	IOFileTypeObject@ // type
	handle@ // handle
	ObObjectReferenceByHandle ok! fileobject! access!

	if (ok@)
		return
	end

	access@ // access
	ACCESS_READ // permission
	SeCheckAccess ok!

	if (ok@)
		fileobject@ ObObjectDereferenceByPointer drop

		return
	end

	fileobject@ // fileobject
	IOUnmountObject ok!

	fileobject@ ObObjectDereferenceByPointer drop
end

fn IOUnmountObject { fileobject -- ok }
	auto fcb
	fileobject@ IOFile_FileControlBlock + @ fcb!

	auto rs
	HALCPUInterruptDisable rs!

	auto mount
	fcb@ IOFileControlBlock_Mount + @ mount!

	if (mount@ ~~)
		rs@ HALCPUInterruptRestore

		STATUS_NOT_SUPPORTED ok!

		return
	end elseif (fcb@ IOFileControlBlock_Flags + @ IOFCBFLAG_MOUNTBUSY &)
		rs@ HALCPUInterruptRestore

		STATUS_TRY_AGAIN_LATER ok!

		return
	end elseif (fcb@ IOFileControlBlock_ParseCount + @)
		rs@ HALCPUInterruptRestore

		STATUS_TRY_AGAIN_LATER ok!

		return
	end

	IOFCBFLAG_MOUNTBUSY fcb@ IOFileControlBlock_Flags + |=

	rs@ HALCPUInterruptRestore

	auto filesystem
	mount@ IOMount_Filesystem + @ filesystem!

	auto unmountfunc
	filesystem@ IOFilesystem_UnmountFunction + @ unmountfunc!

	if (DEBUGCHECKS)
		if (unmountfunc@ ~~)
			"IOUnmountObject: filesystem had no unmount function\n" KeCrash
		end
	end

	IOMountListLockUnalertable

	mount@ // mount
	unmountfunc@ IOFilesystemUnmountFunction ok!

	if (ok@)
		// failed to unmount

		IOMountListUnlock

		HALCPUInterruptDisable rs!
		IOFCBFLAG_MOUNTBUSY ~ fcb@ IOFileControlBlock_Flags + &=
		rs@ HALCPUInterruptRestore

		return
	end

	// remove from mount list
	auto n
	mount@ IOMount_Next + @ n!

	if (n@)
		mount@ IOMount_Prev + @ n@ IOMount_Prev + !
	end

	auto p
	mount@ IOMount_Prev + @ p!

	if (p@)
		mount@ IOMount_Next + @ p@ IOMount_Next + !
	end else // no prev means we were the head
		mount@ IOMount_Next + @ IOMountListHead!
	end

	1 IOMountCount -=

	IOMountListUnlock

	auto volfile
	mount@ IOMount_VolumeFile + @ volfile!

	mount@ MmFree

	// unmounted successfully

	0 fcb@ IOFileControlBlock_Mount + !

	// unbias reference count of volume file object

	volfile@ ObObjectDereferenceByPointer drop

	HALCPUInterruptDisable rs!
	IOFCBFLAG_MOUNTBUSY ~ fcb@ IOFileControlBlock_Flags + &=
	rs@ HALCPUInterruptRestore
end

fn IOMountUpdateFlags { newflags handle -- ok }
	fnsection "PAGE$text"

	auto fileobject

	auto access

	IOFileTypeObject@ // type
	handle@ // handle
	ObObjectReferenceByHandle ok! fileobject! access!

	if (ok@)
		return
	end

	access@ // access
	ACCESS_READ // permission
	SeCheckAccess ok!

	if (ok@)
		fileobject@ ObObjectDereferenceByPointer drop

		return
	end

	if (newflags@ OSMOUNT_READONLY & ~~)
		access@ // access
		ACCESS_WRITE // permission
		SeCheckAccess ok!

		if (ok@)
			fileobject@ ObObjectDereferenceByPointer drop

			return
		end
	end

	newflags@ // newflags
	fileobject@ // fileobject
	IOMountUpdateFlagsObject ok!

	fileobject@ ObObjectDereferenceByPointer drop
end

fn IOMountUpdateFlagsObject { newflags fileobject -- ok }
	auto fcb
	fileobject@ IOFile_FileControlBlock + @ fcb!

	auto rs
	HALCPUInterruptDisable rs!

	auto mount
	fcb@ IOFileControlBlock_Mount + @ mount!

	if (mount@ ~~)
		rs@ HALCPUInterruptRestore

		STATUS_NOT_SUPPORTED ok!

		return
	end elseif (fcb@ IOFileControlBlock_Flags + @ IOFCBFLAG_MOUNTBUSY &)
		rs@ HALCPUInterruptRestore

		STATUS_TRY_AGAIN_LATER ok!

		return
	end elseif (fcb@ IOFileControlBlock_ParseCount + @)
		rs@ HALCPUInterruptRestore

		STATUS_TRY_AGAIN_LATER ok!

		return
	end

	IOFCBFLAG_MOUNTBUSY fcb@ IOFileControlBlock_Flags + |=

	rs@ HALCPUInterruptRestore

	auto oldflags
	mount@ IOMount_Flags + @ oldflags!

	if (oldflags@ newflags@ ==)
		HALCPUInterruptDisable rs!
		IOFCBFLAG_MOUNTBUSY ~ fcb@ IOFileControlBlock_Flags + &=
		rs@ HALCPUInterruptRestore

		return
	end

	auto filesystem
	mount@ IOMount_Filesystem + @ filesystem!

	auto updatefunc
	filesystem@ IOFilesystem_UpdateFlagsFunction + @ updatefunc!

	if (DEBUGCHECKS)
		if (updatefunc@ ~~)
			"IOMountUpdateFlagsObject: filesystem had no update flags function\n" KeCrash
		end
	end

	newflags@ // newflags
	oldflags@ // oldflags
	mount@ // mount
	updatefunc@ IOFilesystemUpdateFlagsFunction ok!

	if (ok@ ~~)
		newflags@ mount@ IOMount_Flags + !
	end

	HALCPUInterruptDisable rs!
	IOFCBFLAG_MOUNTBUSY ~ fcb@ IOFileControlBlock_Flags + &=
	rs@ HALCPUInterruptRestore
end

fn IOMountGetFilesystemName { buffer handle -- ok }
	fnsection "PAGE$text"

	auto fileobject

	auto access

	IOFileTypeObject@ // type
	handle@ // handle
	ObObjectReferenceByHandle ok! fileobject! access!

	if (ok@)
		return
	end

	access@ // access
	ACCESS_READ // permission
	SeCheckAccess ok!

	if (ok@)
		fileobject@ ObObjectDereferenceByPointer drop

		return
	end

	buffer@ // buffer
	fileobject@ // fileobject
	IOMountGetFilesystemNameObject ok!

	fileobject@ ObObjectDereferenceByPointer drop
end

fn IOMountGetFilesystemNameObject { buffer fileobject -- ok }
	auto fcb
	fileobject@ IOFile_FileControlBlock + @ fcb!

	buffer@ // ptr
	OBNAMEMAX // size
	0 // word
	memset

	auto ipl
	IPLDPC KeIPLRaise ipl!

	auto mount
	fcb@ IOFileControlBlock_Mount + @ mount!

	if (mount@ ~~)
		ipl@ KeIPLLower

		STATUS_NOT_SUPPORTED ok!

		return
	end

	buffer@ // dest
	mount@ IOMount_Filesystem + @ IOFilesystem_Name + @ // src
	OBNAMEMAX 1 -
	strncpy

	ipl@ KeIPLLower
end

fn IOMountQueryAll { buffer maxquery -- count ok }
	fnsection "PAGE$text"

	buffer@ // ptr
	maxquery@ OSMountInformation_SIZEOF * // size
	0 // word
	memset

	IOMountListLock ok!

	if (ok@)
		return
	end

	auto mount
	IOMountListHead@ mount!

	0 count!

	while (mount@)
		if (maxquery@ ~~)
			break
		end

		auto name
		mount@ IOMount_VolumeFile + @ IOFile_OpenedPath + @ name!

		if (name@)
			buffer@ OSMountInformation_VolumeName + // dest
			name@ // src
			OBNAMEMAX 1 - // max
			strncpy
		end else
			0 buffer@ OSMountInformation_VolumeName + sb
		end

		mount@ IOMount_Flags + @ buffer@ OSMountInformation_Flags + !

		1 count +=
		1 maxquery -=
		OSMountInformation_SIZEOF buffer +=
		mount@ IOMount_Next + @ mount!
	end

	IOMountListUnlock
end

fn IOMountGetFlags { mount -- flags }
	mount@ IOMount_Flags + @ flags!
end

fn IOMountSetFlags { flags mount -- }
	flags@ mount@ IOMount_Flags + !
end

fn IOMountSetContext { context mount -- }
	context@ mount@ IOMount_FSContext + !
end

fn IOMountGetContext { mount -- context }
	mount@ IOMount_FSContext + @ context!
end

fn IOMountSetRootFCB { fcb mount -- }
	fcb@ mount@ IOMount_RootFCB + !
end

fn IOMountGetRootFCB { mount -- fcb }
	mount@ IOMount_RootFCB + @ fcb!
end

fn IOFilesystemSyncAll { -- ok }
	fnsection "PAGE$text"

	IOMountListLock ok!

	if (ok@)
		return
	end

	auto mount
	IOMountListHead@ mount!

	while (mount@)
		auto filesystem
		mount@ IOMount_Filesystem + @ filesystem!

		auto flushfunc
		filesystem@ IOFilesystem_FlushFunction + @ flushfunc!

		if (flushfunc@)
			mount@ // mount
			flushfunc@ IOFilesystemFlushFunction drop
		end

		mount@ IOMount_Next + @ mount!
	end

	IOMountListUnlock
end