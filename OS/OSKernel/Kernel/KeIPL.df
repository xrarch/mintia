#include "<df>/dragonfruit.h"

#include "<inc>/HALLog.h"
#include "<inc>/HALCPU.h"

#include "<inc>/Kernel.h"

#include "<inc>/HALInterrupt.h"

// XXX this global will NOT work out well if we ever go multiprocessor.
// though, neither will most things that we're doing.
var KeIPLCurrent IPLHIGH
public KeIPLCurrent

fn KeIPLRaise { newipl -- oldipl }
	KeIPLCurrent@ oldipl!

	if (newipl@ oldipl@ ==)
		return
	end

	if (newipl@ oldipl@ <)
		[newipl@]HALIPLNames@ [oldipl@]HALIPLNames@ "IPL not greater or equal (old=%s new=%s)\n" KeCrash
	end

	// only call into the HAL if hardware interrupt level changed.
	// no need to call in for software interrupt levels IPLLOW-IPLDPC.

	// XXX I don't know if I really need to disable interrupts for these two
	// cases.

	auto rs

	if (newipl@ IPLDPC >)
		HALCPUInterruptDisable rs!
		newipl@ KeIPLCurrent!
		newipl@ HALPlatformIPLSet
		rs@ HALCPUInterruptRestore
	end elseif (oldipl@ IPLDPC >)
		HALCPUInterruptDisable rs!
		newipl@ KeIPLCurrent!
		newipl@ HALPlatformIPLSet
		rs@ HALCPUInterruptRestore
	end else
		newipl@ KeIPLCurrent!
	end
end

fn KeIPLLower { newipl -- }
	auto oldipl
	KeIPLCurrent@ oldipl!

	if (newipl@ oldipl@ ==)
		return
	end

	if (newipl@ oldipl@ >)
		[newipl@]HALIPLNames@ [oldipl@]HALIPLNames@ "IPL not less or equal (old=%s new=%s)\n" KeCrash
	end

	auto rs

	if (newipl@ IPLDPC >=)
		HALCPUInterruptDisable rs!
		newipl@ KeIPLCurrent!
		newipl@ HALPlatformIPLSet
		rs@ HALCPUInterruptRestore

		return
	end

	if (oldipl@ IPLAPC >)
		// the new IPL isn't >=IPLDPC, and the old IPL was >IPLAPC.
		// this means that we've dropped below IPLDPC and may have some DPCs
		// to process.

		// only call into the HAL if hardware interrupt level changed.
		// no need to call in for software interrupt levels IPLLOW-IPLDPC.

		if (oldipl@ IPLDPC >)
			HALCPUInterruptDisable rs!
			IPLDPC KeIPLCurrent!
			IPLDPC HALPlatformIPLSet
			rs@ HALCPUInterruptRestore
		end else
			IPLDPC KeIPLCurrent!
		end

		IPLDPC oldipl!

		HALCPUInterruptDisable rs!

		while (KeDPCListHead@ KeThreadNext@ ||)
			if (KeDPCListHead@)
				rs@ KeDPCDispatchQueue
			end

			if (KeThreadNext@)
				rs@ HALCPUInterruptRestore

				KeThreadNextSwitch

				HALCPUInterruptDisable rs!
			end
		end

		rs@ HALCPUInterruptRestore
	end

	newipl@ KeIPLCurrent!
end

externptr HALInterruptStackTop

var KeInterruptNested 0
public KeInterruptNested

fn (HALKeInterruptF) KeInterrupt { ipl int handler -- }
	auto oldipl
	KeIPLCurrent@ oldipl!

	// inline these things instead of calling KeIPLRaise because we have
	// special knowledge here that lets us be faster.

	KeIPLCurrent@ oldipl!
	ipl@ KeIPLCurrent!
	ipl@ HALPlatformIPLSet

	if (KeInterruptNested@ ~~)
		1 KeInterruptNested!

		int@ handler@ HALInterruptStackTop@ HALCPURunInContext

		0 KeInterruptNested!

		HALCPUInterruptEnable

		if (oldipl@ IPLDPC <)
			oldipl@ KeIPLLower

			HALCPUInterruptDisable drop
		end else
			// not dropping below IPLDPC, we can inline instead.

			HALCPUInterruptDisable drop

			oldipl@ KeIPLCurrent!
			oldipl@ HALPlatformIPLSet
		end
	end else
		HALCPUInterruptEnable

		int@ handler@ HALInterruptHandler

		HALCPUInterruptDisable drop

		// inline what KeIPLLower would do here...

		oldipl@ KeIPLCurrent!
		oldipl@ HALPlatformIPLSet
	end
end