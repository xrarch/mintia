#include "<df>/dragonfruit.h"

#include "<inc>/HALLog.h"
#include "<inc>/HALCPU.h"
#include "<inc>/HALMap.h"
#include "<inc>/HALDebug.h"

#include "<inc>/Kernel.h"

#include "<inc>/Executive.h"

#include "<inc>/Memory.h"

#include "<ll>/OSDLL/OSStatus.h"

buffer MmPoolSpaceBitmapHeader ExBitmapHeader_SIZEOF
public MmPoolSpaceBitmapHeader

buffer MmPoolSpaceBitmap (POOLPAGES 7 + 3 >>)

var MmPoolBytesUsed 0
public MmPoolBytesUsed

externptr MmSlabEvictableFunctions

// The heap is managed within a 32MB region of kernel space called
// "pool space". Allocations less than a page size are handled by a slab
// allocator within pool pages. Allocations of a page size or more are handled
// by directly allocating pool pages.

fn MmPoolInit { -- }
	POOLPAGES // sizeinbits
	MmPoolSpaceBitmap // data	
	MmPoolSpaceBitmapHeader // header
	ExBitmapInitialize
end

fn MmHeapPrintTag { tag -- }
	auto shf
	32 shf!

	while (shf@)
		8 shf -=

		auto c
		tag@ shf@ >> 0xFF & c!

		if (c@ 0x80 & ~~ c@ 0x20 >= &&)
			c@ Putc
		end else
			'!' Putc
		end
	end
end

fn MmHeapCheck { -- }
	"MmHeapCheck: not implemented\n" KeCrash
end

fn MmHeapDumpBlockInfo { block -- }

end

fn MmAllocWithTag { bytes tag -- ptr ok }
	if (DEBUGCHECKS)
		if (MmInited@ ~~)
			"MmAllocWithTag: used before MmInit called\n" KeCrash
		end

		if (KeIPLCurrentGet IPLDPC >)
			"MmAllocWithTag: ipl > IPLDPC\n" KeCrash
		end
	end

	// round up to nearest long
	bytes@ 3 + 3 ~ & bytes!

	if (bytes@ MmPoolSlab_SIZEOF + PAGESIZE 2 / >=)
		bytes@ // bytes
		tag@ // tag
		MmPoolPageAlignedAlloc ok! ptr! drop

		return
	end

	bytes@ // bytes
	tag@ // tag
	MmSlabAlloc ok! ptr!
end

fn MmFree { ptr -- }
	if (DEBUGCHECKS)
		if (MmInited@ ~~)
			"MmFree: used before MmInit called\n" KeCrash
		end

		if (ptr@ -1 ==)
			"MmFree: tried to free -1 pointer\n" KeCrash
		end

		if (KeIPLCurrentGet IPLDPC >)
			"MmFree: ipl > IPLDPC\n" KeCrash
		end
	end

	if (ptr@ ~~)
		"MmFree: tried to free null pointer\n" KeCrash
	end

	if (ptr@ PAGEOFFSETMASK & ~~)
		// page aligned.

		ptr@ // ptr
		MmPoolPageAlignedFree

		return
	end

	ptr@ MmSlabFree
end

var MmPoolPageHint 0

fn MmPoolSpaceReserve { pagesneeded -- offset ok }
	auto bmpheader
	MmPoolSpaceBitmapHeader bmpheader!

	auto ipl
	IPLDPC KeIPLRaise ipl!

	MmPoolPageHint@ // hint
	pagesneeded@ // runlength
	bmpheader@ // header
	ExBitmapFindRun ok! offset!

	if (ok@)
		ipl@ KeIPLLower

		STATUS_NO_MEMORY ok!

		return
	end

	offset@ MmPoolPageHint!

	pagesneeded@ // runlength
	offset@ // index
	bmpheader@ // header
	ExBitmapSetBits

	ipl@ KeIPLLower
end

fn MmPoolSpaceRelease { pages offset -- }
	auto ipl
	IPLDPC KeIPLRaise ipl!

	pages@ // runlength
	offset@ // index
	MmPoolSpaceBitmapHeader // header
	ExBitmapClearBits

	ipl@ KeIPLLower
end

fn MmPoolPageAlignedAlloc { bytes tag -- pfdbe ptr ok }
	auto pagesneeded
	bytes@ PAGEOFFSETMASK + PAGESHIFT >> pagesneeded!

	auto offset

	pagesneeded@ // pagesneeded
	MmPoolSpaceReserve ok! offset!

	if (ok@)
		return
	end

	// the vaddrs are marked allocated, so we don't need to be in IPLDPC now,
	// since nobody else is going to touch this range.

	auto vaddr
	offset@ PAGESHIFT << POOLSPACE + vaddr!

	vaddr@ ptr!

	auto kdir
	HALPlatformKernelPageDirectory@ kdir!

	auto i
	0 i!

	auto firstpfdbe
	0 firstpfdbe!

	while (i@ pagesneeded@ <)
		auto phyaddr

		FREEFIRST MUSTSUCCEEDL2 | // priority
		MmPageAlloc ok! phyaddr! pfdbe!

		if (ok@)
			// failed to allocate... gotta go back and free/unmap those pages

			while (i@)
				PAGESIZE vaddr -=
				1 i -=

				auto uok

				0 // phyaddr
				0 // flags
				vaddr@ // vaddr
				kdir@ // pagemap
				0 // asid
				MmPTEUpdateByVirtual uok! drop phyaddr!

				if (DEBUGCHECKS)
					if (uok@)
						"MmPoolPageAlignedAlloc: failed to unmap\n" KeCrash
					end
				end

				phyaddr@ PAGESHIFT >> MmPageFree
			end

			pagesneeded@ // pages
			offset@ // offset
			MmPoolSpaceRelease

			return
		end

		if (firstpfdbe@ ~~)
			pfdbe@ firstpfdbe!
		end

		phyaddr@ PAGESHIFT << // phyaddr
		PTE_G PTE_K | PTE_W | PTE_V | // flags
		vaddr@ // vaddr
		kdir@ // pagemap
		0 // asid
		MmPTEUpdateByVirtual ok! drop drop

		if (DEBUGCHECKS)
			if (ok@)
				"MmPoolPageAlignedAlloc: failed to map virtual address\n" KeCrash
			end
		end

		tag@ pfdbe@ MmPageFrameEntryPool_Tag + !
		pagesneeded@ PAGESHIFT << pfdbe@ MmPageFrameEntryPool_ByteSize + !
		vaddr@ pfdbe@ MmPageFrameEntryPool_VirtualAddress + !

		PAGESIZE vaddr +=
		1 i +=
	end

	auto rs
	HALCPUInterruptDisable rs!
	pagesneeded@ PAGESHIFT << MmPoolBytesUsed +=
	rs@ HALCPUInterruptRestore

	firstpfdbe@ pfdbe!
end

fn MmPoolPageAlignedFree { ptr -- }
	if (DEBUGCHECKS)
		if (ptr@ POOLSPACE <)
			ptr@ "MmPoolPageAlignedFree: ptr 0x%08x < POOLSPACE\n" KeCrash
		end

		if (ptr@ POOLSPACE POOLSIZE + >=)
			ptr@ "MmPoolPageAlignedFree: ptr 0x%08x beyond pool space\n" KeCrash
		end

		if (ptr@ 3 &)
			ptr@ "MmPoolPageAlignedFree: ptr 0x%08x not aligned\n" KeCrash
		end
	end

	auto offset
	ptr@ POOLSPACE - PAGESHIFT >> offset!

	auto pfdbe

	ptr@ MmPoolPageGetPhysical pfdbe! drop

	auto pages
	pfdbe@ MmPageFrameEntryPool_ByteSize + @ PAGEOFFSETMASK + PAGESHIFT >> pages!

	auto kdir
	HALPlatformKernelPageDirectory@ kdir!

	auto i
	0 i!

	while (i@ pages@ <)
		auto phyaddr
		auto ok

		0 // phyaddr
		0 // flags
		ptr@ // vaddr
		kdir@ // pagemap
		0 // asid
		MmPTEUpdateByVirtual ok! drop phyaddr!

		if (DEBUGCHECKS)
			if (ok@)
				"MmPoolPageAlignedFree: failed to unmap\n" KeCrash
			end
		end

		phyaddr@ PAGESHIFT >> MmPageFree

		PAGESIZE ptr +=
		1 i +=
	end

	auto bmpheader
	MmPoolSpaceBitmapHeader bmpheader!

	auto rs
	HALCPUInterruptDisable rs!
	pages@ PAGESHIFT << MmPoolBytesUsed -=
	rs@ HALCPUInterruptRestore

	pages@ // pages
	offset@ // offset
	MmPoolSpaceRelease
end

fn MmChargeBytesGet { bytes -- charge }
	bytes@ MmPoolSlab_SIZEOF + charge!

	if (charge@ PAGESIZE >=)
		bytes@ PAGEOFFSETMASK + PAGENUMBERMASK & charge!
	end else
		1 MMSLABPO2START << charge!

		while (charge@ bytes@ <)
			1 charge <<=
		end
	end
end

fn MmBlockChargeGet { block -- charge }
	if (DEBUGCHECKS)
		if (block@ 3 &)
			block@ "MmBlockChargeGet: ptr 0x%08x not aligned\n" KeCrash
		end
	end

	if (block@ PAGEOFFSETMASK & ~~)
		if (DEBUGCHECKS)
			if (block@ POOLSPACE <)
				block@ "MmBlockChargeGet: ptr 0x%08x < POOLSPACE\n" KeCrash
			end

			if (block@ POOLSPACE POOLSIZE + >=)
				block@ "MmBlockChargeGet: ptr 0x%08x beyond pool space\n" KeCrash
			end
		end

		// page aligned. charge is defined by the pfdbe.

		auto pfdbe
		block@ MmPoolPageGetPhysical pfdbe! drop

		pfdbe@ MmPageFrameEntryPool_ByteSize + @ charge!

		return
	end

	// not page aligned. charge is defined by pool header.

	MmPoolSlab_SIZEOF block -=

	if (block@ MmPoolSlab_Magic + gi MMSLABSWIFTMAGIC ~=)
		block@ MmPoolSlab_Magic + gi
		block@ "MmBlockChargeGet: ptr 0x%08x had bad magic 0x%08x\n" KeCrash
	end

	1 MMSLABPO2START block@ MmPoolSlab_BucketIndex + gb + << charge!
end

fn MmPoolPageGetPhysical { ptr -- pfn pfdbe }
	if (DEBUGCHECKS)
		if (ptr@ POOLSPACE <)
			ptr@ "MmPoolPageGetPhysical: ptr 0x%08x < POOLSPACE\n" KeCrash
		end

		if (ptr@ POOLSPACE POOLSIZE + >=)
			ptr@ "MmPoolPageGetPhysical: ptr 0x%08x beyond pool space\n" KeCrash
		end

		if (ptr@ 3 &)
			ptr@ "MmPoolPageGetPhysical: ptr 0x%08x not aligned\n" KeCrash
		end
	end

	auto pteaddr
	auto phyaddr
	auto ok

	ptr@ // vaddr
	HALPlatformKernelPageDirectory@ // pagemap
	MmVirtualtoPTEAddress ok! pteaddr!

	if (DEBUGCHECKS)
		if (ok@)
			ok@ ptr@ "MmPoolPageGetPhysical: ptr %08x wasn't mapped 1 (%i)\n" KeCrash
		end
	end

	pteaddr@ // pteaddr
	MmPTEInterpret ok! drop phyaddr!

	if (DEBUGCHECKS)
		if (ok@)
			ok@ ptr@ "MmPoolPageGetPhysical: ptr %08x wasn't mapped 2 (%i)\n" KeCrash
		end
	end

	phyaddr@ PAGESHIFT >> pfn!

	pfn@ MmPageFrameEntry_SIZEOF * MmPageFrameDatabase@ + pfdbe!
end