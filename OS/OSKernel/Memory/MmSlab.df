#include "<df>/dragonfruit.h"

#include "<inc>/HALLog.h"
#include "<inc>/HALCPU.h"
#include "<inc>/HALMap.h"
#include "<inc>/HALDebug.h"

#include "<inc>/Kernel.h"

#include "<inc>/Executive.h"

#include "<inc>/Memory.h"

#include "<ll>/OSDLL/OSStatus.h"

// slab allocation functions. should be called only through MmAllocWithTag and
// MmFree.

table MmSlabEvictableFunctions
	pointerof MmSlabEvictionFunction // EvictionFunc
	0 // ReferenceFunc
	0 // DereferenceFunc
	0 // ModifyFunc
	0 // Reserved2
	0 // Reserved3
	0 // Reserved4
	0 // Reserved5
endtable
public MmSlabEvictableFunctions

var MmSlabBytesUsedExternally 0
public MmSlabBytesUsedExternally

var MmSlabBytesUsedInternally 0
public MmSlabBytesUsedInternally

table MmSlabListHeads[MMSLABBUCKETS]

fn MmSlabAlloc { bytes tag -- ptr ok }
	0 ok!

	// calculate as much stuff as possible here before we disable interrupts

	MmPoolSlab_SIZEOF bytes +=

	auto sz
	1 MMSLABPO2START << sz!

	auto bucketindex
	0 bucketindex!
	
	while (sz@ bytes@ <)
		1 sz <<=
		1 bucketindex +=
	end

	auto slabsperpage
	PAGESIZE bucketindex@ MMSLABPO2START + >> 1 - slabsperpage!

	if (DEBUGCHECKS)
		if (bucketindex@ MMSLABBUCKETS >=)
			"MmSlabAlloc: bucketindex too big\n" KeCrash
		end
	end

	auto pfdbe

	auto listhead
	[bucketindex@]MmSlabListHeads listhead!

	auto rs
	HALCPUInterruptDisable rs!

	listhead@@ ptr!

	if (ptr@)
		if (ptr@ MmPoolSlab_Magic + gi MMSLABFREEMAGIC ~=)
			ptr@ MmPoolSlab_Magic + gi
			ptr@ "MmSlabAlloc: slab 0x%08x had bad magic (0x%08x)\n" KeCrash
		end

		if (DEBUGCHECKS)
			if (ptr@ MmPoolSlab_Reserved + gb)
				ptr@ MmPoolSlab_Reserved + gb
				ptr@ "MmSlabAlloc: slab 0x%08x had a non-zero reserved byte (0x%08x)\n" KeCrash
			end

			if (ptr@ MmPoolSlab_BucketIndex + gb bucketindex@ ~=)
				ptr@ MmPoolSlab_BucketIndex + gb
				bucketindex@
				ptr@ "MmSlabAlloc: slab 0x%08x had a weird bucketindex, wanted %d, found %d\n" KeCrash
			end
		end

		ptr@ IDENTITYSPACE ~ & PAGESHIFT >> MmPageFrameEntry_SIZEOF * MmPageFrameDatabase@ + pfdbe!
		pfdbe@ MmEvictablePageReference drop

		auto n
		ptr@ MmPoolSlab_TagOrNextFree + @ n!

		n@ listhead@!

		if (n@)
			0 n@ MmPoolSlab_PrevFree + !
		end

		sz@ MmSlabBytesUsedInternally +=

		rs@ HALCPUInterruptRestore

		tag@ ptr@ MmPoolSlab_TagOrNextFree + !
		MMSLABSWIFTMAGIC ptr@ MmPoolSlab_Magic + si

		MmPoolSlab_SIZEOF ptr +=

		return
	end

	// no slab found, allocate a physical page and turn it into a bunch of
	// properly-sized slabs. dip down into IPLDPC and turn interrupts back
	// on since this just turned into a lengthy operation.

	auto ipl
	IPLDPC KeIPLRaise ipl!

	rs@ HALCPUInterruptRestore

	MMEVICTFLAG_FAST // flags
	MmSlabEvictableFunctions // evictablefuncs
	FREEFIRST MUSTSUCCEEDL2 | // priority
	MmEvictablePageAlloc ok! ptr! pfdbe!

	if (ok@)
		ipl@ KeIPLLower

		return
	end

	bucketindex@ pfdbe@ MmPageFrameEntrySlab_BucketIndex + !

	ptr@ PAGESHIFT << IDENTITYSPACE | ptr!

	if (DEBUGCHECKS)
		0 ptr@ MmPoolSlab_Reserved + sb
	end

	bucketindex@ ptr@ MmPoolSlab_BucketIndex + sb
	0 ptr@ MmPoolSlab_PrevFree + !
	tag@ ptr@ MmPoolSlab_TagOrNextFree + !
	MMSLABSWIFTMAGIC ptr@ MmPoolSlab_Magic + si

	// store current slab pointer in pfdbe to avoid saving another auto in the
	// function prologue
	ptr@ sz@ + pfdbe!

	// store last slab pointer in ok for same reason as above
	listhead@@ ok!

	while (slabsperpage@)
		if (DEBUGCHECKS)
			0 pfdbe@ MmPoolSlab_Reserved + sb
		end

		bucketindex@ pfdbe@ MmPoolSlab_BucketIndex + sb
		ok@ pfdbe@ MmPoolSlab_TagOrNextFree + !
		MMSLABFREEMAGIC pfdbe@ MmPoolSlab_Magic + si

		if (ok@)
			pfdbe@ ok@ MmPoolSlab_PrevFree + !
		end

		pfdbe@ ok!
		1 slabsperpage -=
		sz@ pfdbe +=
	end

	sz@ pfdbe -=
	pfdbe@ listhead@!

	PAGESIZE MmSlabBytesUsedExternally +=
	sz@ MmSlabBytesUsedInternally +=

	ipl@ KeIPLLower

	MmPoolSlab_SIZEOF ptr +=

	0 ok!
end

fn MmSlabFree { ptr -- }
	MmPoolSlab_SIZEOF ptr -=

	if (ptr@ MmPoolSlab_Magic + gi MMSLABSWIFTMAGIC ~=)
		ptr@ MmPoolSlab_Magic + gi
		ptr@ "MmSlabFree: slab 0x%08x had bad magic (0x%08x)\n" KeCrash
	end

	if (DEBUGCHECKS)
		if (ptr@ MmPoolSlab_Reserved + gb)
			ptr@ MmPoolSlab_Reserved + gb
			ptr@ "MmSlabFree: slab 0x%08x had a non-zero reserved byte (0x%08x)\n" KeCrash
		end
	end

	auto pfn
	ptr@ IDENTITYSPACE ~ & PAGESHIFT >> pfn!

	auto pfdbe
	pfn@ MmPageFrameEntry_SIZEOF * MmPageFrameDatabase@ + pfdbe!

	auto bucketindex
	ptr@ MmPoolSlab_BucketIndex + gb bucketindex!

	auto h

	auto listhead
	[bucketindex@]MmSlabListHeads listhead!

	listhead@@ h!

	MMSLABFREEMAGIC ptr@ MmPoolSlab_Magic + si

	auto sz
	1 MMSLABPO2START bucketindex@ + << sz!

	auto rs
	HALCPUInterruptDisable rs!

	h@ ptr@ MmPoolSlab_TagOrNextFree + !
	ptr@ listhead@!

	if (h@)
		ptr@ h@ MmPoolSlab_PrevFree + !
	end

	pfdbe@ MmEvictablePageDereference drop

	sz@ MmSlabBytesUsedInternally -=

	rs@ HALCPUInterruptRestore
end

fn (MmEvictionFunction) MmSlabEvictionFunction { pfdbe -- dropped evicted ok }
	0 ok!
	0 dropped!
	1 evicted!

	// we need to unlink all the free slabs from the proper list

	auto bucketindex
	pfdbe@ MmPageFrameEntrySlab_BucketIndex + @ bucketindex!

	auto listhead
	[bucketindex@]MmSlabListHeads listhead!

	auto slab
	pfdbe@ MmPageFrameEntry_PFN + @ PAGESHIFT << IDENTITYSPACE | slab!

	auto sz
	1 MMSLABPO2START bucketindex@ + << sz!

	auto slabsperpage
	PAGESIZE bucketindex@ MMSLABPO2START + >> slabsperpage!

	while (slabsperpage@)
		if (slab@ MmPoolSlab_Magic + gi MMSLABFREEMAGIC ~=)
			slab@ MmPoolSlab_Magic + gi
			slab@ MmPoolSlab_TagOrNextFree + @
			slab@
			"MmSlabEvictionFunction: 0x%08x (0x%08x): bad magic 0x%04x\n" KeCrash
		end

		auto ls
		slab@ MmPoolSlab_PrevFree + @ ls!

		auto ns
		slab@ MmPoolSlab_TagOrNextFree + @ ns!

		if (ls@)
			ns@ ls@ MmPoolSlab_TagOrNextFree + !
		end else
			ns@ listhead@!
		end

		if (ns@)
			ls@ ns@ MmPoolSlab_PrevFree + !
		end

		sz@ slab +=
		1 slabsperpage -=
	end
end