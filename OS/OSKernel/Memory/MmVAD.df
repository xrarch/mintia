#include "<df>/dragonfruit.h"

#include "<inc>/HALLog.h"
#include "<inc>/HALCPU.h"
#include "<inc>/HALMap.h"

#include "<inc>/Kernel.h"

#include "<inc>/Executive.h"

#include "<inc>/Memory.h"

#include "<inc>/Security.h"

#include "<inc>/Object.h"

#include "<inc>/IO.h"

#include "<inc>/Process.h"

#include "<ll>/OSDLL/OS.h"

var MmVADListHead 0
public MmVADListHead

var MmVADListTail 0
public MmVADListTail

// routines for creating, destroying, and manipulating
// Virtual Address Descriptors (VADs).

fn MmVADCreate { startva endva prot offset processobject sectionobject quotablock -- vad ok }
	// allocates a VAD structure.
	// references the section object, and the quotablock if applicable.

	quotablock@ MmQuotaBlockReference

	auto qcharge
	MmVAD_SIZEOF MmChargeBytesGet qcharge!

	qcharge@ // charge
	quotablock@ // quotablock
	MmQuotaBlockCharge ok!

	if (ok@)
		return
	end

	MmVAD_SIZEOF // bytes
	'nVAD' // tag
	MmAllocWithTag ok! vad!

	if (ok@)
		qcharge@ // charge
		quotablock@ // quotablock
		MmQuotaBlockUncharge

		return
	end

	if (prot@ PAGEACCESS_WRITECOPY &)
		// charge vm quota for this VAD because it has the potential to
		// allocate lots of anonymous pages via COW

		endva@ startva@ - // charge
		quotablock@ // quotablock
		MmQuotaBlockChargeVM ok!

		if (ok@)
			qcharge@ // charge
			quotablock@ // quotablock
			MmQuotaBlockUncharge

			return
		end

		1 vad@ MmVAD_ChargeVMQuota + !
	end else
		0 vad@ MmVAD_ChargeVMQuota + !
	end

	sectionobject@ ObObjectReferenceByPointer drop

	sectionobject@ vad@ MmVAD_SectionObject + !
	offset@ vad@ MmVAD_OffsetInSection + !

	0 vad@ MmVAD_Next + !
	0 vad@ MmVAD_Prev + !
	startva@ vad@ MmVAD_StartVA + !
	endva@ vad@ MmVAD_EndVA + !
	0 vad@ MmVAD_FurthestExtentMapped + !
	prot@ vad@ MmVAD_PageProtection + !
	quotablock@ vad@ MmVAD_QuotaBlock + !
	processobject@ vad@ MmVAD_ProcessObject + !
end

fn MmVADListLock { processobject -- ok }
	KERNELMODE // waitmode
	1 // alertable
	OSWAIT_TIMEOUTINFINITE // timeout
	processobject@ PsProcess_VADListMutex + // object
	KeThreadWaitForObject ok!
end

fn MmVADListTryLock { processobject -- ok }
	KERNELMODE // waitmode
	0 // alertable
	0 // timeout
	processobject@ PsProcess_VADListMutex + // object
	KeThreadWaitForObject ok!
end

fn MmVADListIsLocked { processobject -- locked }
	processobject@ PsProcess_VADListMutex + // mutex
	KeMutexIsLocked locked!
end

fn MmVADListUnlock { processobject -- }
	processobject@ PsProcess_VADListMutex + KeMutexRelease drop
end

fn MmVADFind { vaddr length mustbestart processobject -- vad ok }
	// locate the VAD containing the given virtual address. if mustbestart is
	// true, then the virtual address must be at the start of the VAD, and the
	// length must match the length of the VAD, or else an error is returned.
	// if it is false, the length argument is completely ignored.

	// assumes the caller locked the process VAD list.

	processobject@ PsProcess_VADLookupHint + @ vad!

	if (vad@ ~~)
		// no hint, start at listhead
		processobject@ PsProcess_VADListHead + @ vad!
	end

	0 ok!

	// fast check for if the hint is immediately the correct VAD (common)
	if (vaddr@ vad@ MmVAD_StartVA + @ >=)
		if (vaddr@ vad@ MmVAD_EndVA + @ <)
			// found it

			if (mustbestart@)
				if (vad@ MmVAD_StartVA + @ vaddr@ ~=)
					STATUS_BAD_ADDRESS ok!

					return
				end

				if (vad@ MmVAD_EndVA + @ vad@ MmVAD_StartVA + @ - length@ ~=)
					STATUS_BAD_ADDRESS ok!

					return
				end
			end else
				vad@ processobject@ PsProcess_VADLookupHint + !
			end

			return
		end
	end

	// hint was not the correct VAD, check if it is before or after the hint
	// and search in the appropriate direction.

	if (vaddr@ vad@ MmVAD_StartVA + @ >)
		// after the hint, search forwards

		while (vad@)
			if (vaddr@ vad@ MmVAD_StartVA + @ <)
				// vaddr was before this VAD which means we missed it, which
				// means it's not mapped

				STATUS_BAD_ADDRESS ok!

				return
			end

			if (vaddr@ vad@ MmVAD_EndVA + @ <)
				// found it!

				if (mustbestart@ ~~)
					vad@ processobject@ PsProcess_VADLookupHint + !
				end

				break
			end

			vad@ MmVAD_Next + @ vad!
		end
	end else
		// before the hint, search backwards

		while (vad@)
			if (vaddr@ vad@ MmVAD_EndVA + @ >=)
				// vaddr was after this VAD which means we missed it, which
				// means it's not mapped

				STATUS_BAD_ADDRESS ok!

				return
			end

			if (vaddr@ vad@ MmVAD_StartVA + @ >=)
				// found it!

				if (mustbestart@ ~~)
					vad@ processobject@ PsProcess_VADLookupHint + !
				end

				break
			end

			vad@ MmVAD_Prev + @ vad!
		end
	end

	if (vad@ ~~)
		STATUS_BAD_ADDRESS ok!
	end elseif (mustbestart@)
		if (vad@ MmVAD_StartVA + @ vaddr@ ~=)
			STATUS_BAD_ADDRESS ok!

			return
		end

		if (vad@ MmVAD_EndVA + @ vad@ MmVAD_StartVA + @ - length@ ~=)
			STATUS_BAD_ADDRESS ok!

			return
		end
	end
end

fn MmVADFindAndMapFreeRegion { viewsize startva fixedva pageprotection sectionoffset sectionobject processobject -- realva ok }
	// find a fitting region with a preferred virtual address of startva.
	// if fixedva is true, then *only* this virtual address can be used; if
	// it is not available an error will be returned.
	// this region must be completely empty.

	auto endva
	// no overflow check, caller should have done that
	startva@ viewsize@ + endva!

	// stores last VAD that can hold the view after it.
	// -1 means no space available, 0 means very start
	auto lastsufficientvad
	-1 lastsufficientvad!

	auto lastvad
	0 lastvad!

	auto lastend
	PAGESIZE lastend!

	0 realva!

	processobject@ MmVADListLock ok!

	if (ok@)
		return
	end

	auto vad
	processobject@ PsProcess_VADListHead + @ vad!

	while (vad@)
		if (fixedva@ ~~)
			if (vad@ MmVAD_StartVA + @ lastend@ - viewsize@ >=)
				// the last space is big enough

				lastend@ realva!
				lastvad@ lastsufficientvad!

				if (startva@ ~~)
					// no startva preference, use it
					break
				end elseif (lastend@ startva@ >=)
					// there's a startva preference but we already went past it
					// so just roll with this
					break
				end
			end
		end

		if (vad@ MmVAD_StartVA + @ endva@ >=)
			if (startva@ lastend@ >=)
				// found our preferred address!
				startva@ realva!
				lastvad@ lastsufficientvad!
				break
			end elseif (fixedva@)
				// this VAD begins after our requested region, which means it
				// either lies directly behind this VAD or doesn't exist.
				// this startva was mandatory...

				processobject@ MmVADListUnlock

				STATUS_UNAVAILABLE_ADDRESS ok!

				return
			end
		end

		vad@ MmVAD_EndVA + @ lastend!
		vad@ lastvad!
		vad@ MmVAD_Next + @ vad!
	end

	if (lastsufficientvad@ -1 ==)
		// see if it can fit in the end

		if (fixedva@)
			// skip a check against MMHIGHESTUSERADDRESS because endva should
			// have been sanity checked against it already by the caller

			if (startva@ lastend@ >=)
				// found a space!
				startva@ realva!
				lastvad@ lastsufficientvad!
			end
		end elseif (MMHIGHESTUSERADDRESS 1 + lastend@ - viewsize@ >=)
			// fits!
			lastend@ realva!
			lastvad@ lastsufficientvad!
		end

		if (lastsufficientvad@ -1 ==)
			processobject@ MmVADListUnlock

			// still -1, didn't fit anywhere!

			STATUS_UNAVAILABLE_ADDRESS ok!

			return
		end
	end

	// create the VAD for this mapping and insert it in the list just after
	// lastsufficientvad (or before the head of the list if lastsufficientvad
	// is 0)

	realva@ // startva
	realva@ viewsize@ + // endva
	pageprotection@ // prot
	sectionoffset@ // offset
	processobject@ // processobject
	sectionobject@ // sectionobject
	processobject@ PsProcess_QuotaBlock + @ // quotablock
	MmVADCreate ok! vad!
	
	if (ok@)
		processobject@ MmVADListUnlock

		return
	end

	auto h

	if (lastsufficientvad@ ~~)
		// insert at head

		processobject@ PsProcess_VADListHead + @ h!

		if (h@)
			vad@ h@ MmVAD_Prev + !
			h@ vad@ MmVAD_Next + !
		end else
			vad@ processobject@ PsProcess_VADListTail + !
		end

		vad@ processobject@ PsProcess_VADListHead + !
	end else
		// insert ahead of given VAD

		lastsufficientvad@ MmVAD_Next + @ h!

		if (h@)
			vad@ h@ MmVAD_Prev + !
			h@ vad@ MmVAD_Next + !
		end else
			vad@ processobject@ PsProcess_VADListTail + !
		end

		lastsufficientvad@ vad@ MmVAD_Prev + !
		vad@ lastsufficientvad@ MmVAD_Next + !
	end

	// insert at tail of global VAD list (used for evictions)
	vad@ MmVADInsert

	processobject@ MmVADListUnlock
end

fn MmVADInsert { vad -- }
	auto rs
	HALCPUInterruptDisable rs!

	auto t
	MmVADListTail@ t!

	if (t@ ~~)
		0 vad@ MmVAD_NextGlobal + !
		0 vad@ MmVAD_PrevGlobal + !

		vad@ MmVADListHead!
		vad@ MmVADListTail!
	end else
		0 vad@ MmVAD_NextGlobal + !

		t@ vad@ MmVAD_PrevGlobal + !
		vad@ t@ MmVAD_NextGlobal + !
		vad@ MmVADListTail!
	end

	rs@ HALCPUInterruptRestore
end

fn MmVADRemove { vad -- }
	auto rs
	HALCPUInterruptDisable rs!

	auto ls
	vad@ MmVAD_PrevGlobal + @ ls!

	auto ns
	vad@ MmVAD_NextGlobal + @ ns!

	if (ls@)
		ns@ ls@ MmVAD_NextGlobal + !
	end else
		ns@ MmVADListHead!
	end

	if (ns@)
		ls@ ns@ MmVAD_PrevGlobal + !
	end else
		ls@ MmVADListTail!
	end

	0 vad@ MmVAD_PrevGlobal + !
	0 vad@ MmVAD_NextGlobal + !

	rs@ HALCPUInterruptRestore
end

fn MmVADUnmap { vad processobject -- }
	// assumes the caller locked the process VAD list.

	// unmap all the pages associated with the VAD, then remove it from the
	// process VAD list and destroy it.

	// does NOT dereference the section object, caller must do that.

	if (DEBUGCHECKS)
		if (KeThreadCurrent@ KeThread_IgnoreKill + @ ~~)
			"MmVADUnmap: could be killed\n" KeCrash
		end
	end

	auto pdir
	processobject@ PsProcess_PCB + KeProcess_PageDirectory + @ pdir!

	auto asid
	processobject@ PsProcess_PCB + KeProcess_ASID + @ asid!

	auto vaddr
	vad@ MmVAD_StartVA + @ vaddr!

	auto max
	// only unmap up to the highest vaddr that was ever faulted in, as an
	// optimization.
	vad@ MmVAD_FurthestExtentMapped + @ max!

	if (DEBUGCHECKS)
		if (vaddr@ PAGEOFFSETMASK &)
			vaddr@ "MmVADUnmap: vaddr wasn't page-aligned (0x%08x)\n" KeCrash
		end

		if (max@ PAGEOFFSETMASK &)
			max@ "MmVADUnmap: max wasn't page-aligned (0x%08x)\n" KeCrash
		end

		if (vaddr@ MMHIGHESTUSERADDRESS >)
			vaddr@ "MmVADUnmap: vaddr > MMHIGHESTUSERADDRESS (0x%08x)\n" KeCrash
		end
	end

	auto commitcharge
	vad@ MmVAD_SectionObject + @ MmSection_NoCommit + @ ~~ commitcharge!

	auto ok

	auto phyaddr
	auto pteaddr
	auto flags
	auto pte

	auto pfdbe

	while (vaddr@ max@ <=)
		// XXX this is actually BROKEN with modified PTEs!
		// it leaves the pointer from the modified PFDBE to
		// this PTE intact which is incorrect! I will fix this
		// as part of the working set overhaul (which is
		// getting moved WAY up on the schedule)

		vaddr@ // vaddr
		pdir@ // pagemap
		MmVirtualtoPTEAddress ok! pteaddr!

		if (ok@ ~~)
			pteaddr@@ pte!

			0 // phyaddr
			0 // flags
			vaddr@ // vaddr
			pteaddr@ // pteaddr
			asid@ // asid
			MmPTEUpdate ok! flags! phyaddr!

			if (flags@ PTE_V &)
				// unmapped a page, do accounting

				if (commitcharge@)
					1 processobject@ PsProcess_CommittedPageFrames + -=

					phyaddr@ PAGESHIFT >> MmPageFrameEntry_SIZEOF * MmPageFrameDatabase@ + pfdbe!

					// dereference page or free immediately if COW

					if (pfdbe@ MmPageFrameEntryEvictable_EvictionFlags + @ MMEVICTFLAG_COW &)
						// is COW, free now

						phyaddr@ PAGESHIFT >> MmPageFree
					end else
						// not COW, deref

						pfdbe@ MmEvictablePageDereference drop
					end
				end

				1 processobject@ PsProcess_MappedPageFrames + -=
			end else
				if (pte@ PTE_INSWAP &)
					// free COW page in swap

					pte@ PAGESHIFT >> IOSwapPageFree
				end
			end

			if (pte@)
				// there was a PTE containing some information, so delete it.

				pteaddr@ // pteaddr
				vaddr@ // vaddr
				pdir@ // pagemap
				MmPTEDelete drop
			end
		end

		PAGESIZE vaddr +=
	end

	if (vad@ processobject@ PsProcess_VADLookupHint + @ ==)
		0 processobject@ PsProcess_VADLookupHint + !
	end

	if (vad@ processobject@ PsProcess_VADMapHint + @ ==)
		0 processobject@ PsProcess_VADMapHint + !
	end

	auto n
	vad@ MmVAD_Next + @ n!

	if (n@)
		vad@ MmVAD_Prev + @ n@ MmVAD_Prev + !
	end else // no next means we were the tail
		vad@ MmVAD_Prev + @ processobject@ PsProcess_VADListTail + !
	end

	vad@ MmVAD_Prev + @ n!

	if (n@)
		vad@ MmVAD_Next + @ n@ MmVAD_Next + !
	end else // no prev means we were the head
		vad@ MmVAD_Next + @ processobject@ PsProcess_VADListHead + !
	end

	auto quotablock
	vad@ MmVAD_QuotaBlock + @ quotablock!

	MmVAD_SIZEOF MmChargeBytesGet // charge
	quotablock@ // quotablock
	MmQuotaBlockUncharge

	if (vad@ MmVAD_ChargeVMQuota + @)
		vad@ MmVAD_EndVA + @ vad@ MmVAD_StartVA + @ - // charge
		quotablock@ // quotablock
		MmQuotaBlockUnchargeVM
	end

	quotablock@ MmQuotaBlockDereference

	auto sectionobject
	vad@ MmVAD_SectionObject + @ sectionobject!

	sectionobject@ MmSectionUnprepare
	sectionobject@ ObObjectDereferenceByPointer drop

	vad@ MmVADRemove

	vad@ MmFree
end