//
// Implements executive object creation.
//

#include "<df>/dragonfruit.h"

#include "<inc>/HALLog.h"
#include "<inc>/HALCPU.h"

#include "<inc>/Kernel.h"

#include "<inc>/Executive.h"

#include "<inc>/Object.h"

#include "<inc>/Memory.h"

#include "<inc>/Security.h"

#include "<inc>/Process.h"

#include "<ll>/OSDLL/OS.h"

fn ObObjectCreate { name flags permissions quotabias bodysize type -- object ok }
	0 ok!

	auto tag
	if (type@)
		type@ ObType_Tag + @ tag!
	end else
		'Objt' tag!
	end

	bodysize@ ObHeader_SIZEOF + // bytes
	tag@ // tag
	CANBLOCK // flags
	MmAllocWithTag ok! object!

	if (ok@)
		return
	end

	auto qcharge
	object@ MmBlockChargeGet qcharge!

	auto namebuf
	0 namebuf!

	if (name@)
		auto namelen
		name@ strlen namelen!

		if (namelen@ OBNAMEMAX 1 - >=)
			object@ MmFree

			STATUS_NAME_TOO_LONG ok!

			return
		end

		namelen@ 1 + // bytes
		'ObNm' // tag
		CANBLOCK // flags
		MmAllocWithTag ok! namebuf!

		if (ok@)
			object@ MmFree

			return
		end

		namebuf@ name@ strcpy

		namebuf@ MmBlockChargeGet qcharge +=
	end

	quotabias@ qcharge +=

	type@ object@ ObHeader_TypeObject + !

	0 object@ ObHeader_DirectoryListPrev + !
	0 object@ ObHeader_DirectoryListNext + !

	0 object@ ObHeader_DirectoryObject + !

	flags@ object@ ObHeader_Flags + !

	0 object@ ObHeader_HandleCount + !
	1 object@ ObHeader_PointerCount + !

	auto curproc
	KeProcessCurrent curproc!

	curproc@ PsProcess_UID + @ object@ ObHeader_UID + !
	curproc@ PsProcess_GID + @ object@ ObHeader_GID + !
	permissions@ object@ ObHeader_Permissions + !

	qcharge@ object@ ObHeader_QuotaCharge + !
	0 object@ ObHeader_QuotaBlock + !

	namebuf@ object@ ObHeader_Name + !

	auto h

	0 object@ ObHeader_TypeListPrev + !
	0 object@ ObHeader_TypeListNext + !

	if (type@)
		// insert in type list

		auto rs
		HALCPUInterruptDisable rs!

		type@ ObType_ObjectListHead + @ h!

		if (h@)
			h@ object@ ObHeader_TypeListNext + !
			object@ h@ ObHeader_TypeListPrev + !
		end

		object@ type@ ObType_ObjectListHead + !

		rs@ HALCPUInterruptRestore
	end

	ObHeader_SIZEOF object +=
end

fn ObObjectFree { object -- ok }
	0 ok!

	ObHeader_SIZEOF object -=

	if (DEBUGCHECKS)
		if (object@ ObHeader_Flags + @ OBFLAGS_PERMANENT &)
			"ObObjectFree: attempt to free PERMANENT object\n" KeCrash
		end

		if (object@ ObHeader_HandleCount + @)
			"ObObjectFree: attempt to free object with nonzero handle count\n" KeCrash
		end

		if (object@ ObHeader_PointerCount + @)
			"ObObjectFree: attempt to free object with nonzero pointer count\n" KeCrash
		end
	end

	auto type
	object@ ObHeader_TypeObject + @ type!

	if (type@)
		auto deletefunc

		type@ ObType_DeleteFunction + @ deletefunc!

		if (deletefunc@)
			object@ ObHeader_SIZEOF + deletefunc@ ObTypeDeleteFunction
		end
	end

	ObHeader_SIZEOF object +=

	object@ ObObjectDeallocate
end

fn ObObjectDeallocate { object -- }
	ObHeader_SIZEOF object -=

	auto type
	object@ ObHeader_TypeObject + @ type!

	if (type@)
		// remove from type list

		auto rs
		HALCPUInterruptDisable rs!

		auto n
		object@ ObHeader_TypeListNext + @ n!

		auto l
		object@ ObHeader_TypeListPrev + @ l!

		if (n@)
			l@ n@ ObHeader_TypeListPrev + !
		end

		if (l@)
			n@ l@ ObHeader_TypeListNext + !
		end else
			// no prev means we were the listhead

			n@ type@ ObType_ObjectListHead + !
		end

		rs@ HALCPUInterruptRestore
	end

	auto qcharge
	object@ ObHeader_QuotaCharge + @ qcharge!

	auto quotablock
	object@ ObHeader_QuotaBlock + @ quotablock!

	if (object@ ObHeader_Name + @)
		object@ ObHeader_Name + @ MmFree
	end

	object@ MmFree

	if (quotablock@)
		qcharge@ // charge
		quotablock@ // quotablock
		MmQuotaBlockUncharge

		quotablock@ MmQuotaBlockDereference
	end
end

fn ObTypeInitializerInitialize { initializer -- }
	// initialize the initializer.
	// yeah.

	initializer@ // ptr
	ObTypeInitializer_SIZEOF // size
	0 // word
	memset

	-1 initializer@ ObTypeInitializer_WaitOffset + !
end

fn ObTypeCreate { initializer -- type ok }
	auto typetype
	ObTypeObjectType@ typetype!

	initializer@ ObTypeInitializer_Name + @ // name
	OBFLAGS_PERMANENT // flags
	ACCESS_OWNER_ALL ACCESS_GROUP_ALL | // permissions
	0 // quotabias
	initializer@ ObTypeInitializer_BodySize + @ ObType_SIZEOF + // bodysize
	typetype@ // type
	ObObjectCreate ok! type!

	if (ok@)
		return
	end

	initializer@ ObTypeInitializer_Tag + @ type@ ObType_Tag + !

	// initialize function fields
	initializer@ ObTypeInitializer_OpenFunction + @ type@ ObType_OpenFunction + !
	initializer@ ObTypeInitializer_CloseFunction + @ type@ ObType_CloseFunction + !
	initializer@ ObTypeInitializer_DeleteFunction + @ type@ ObType_DeleteFunction + !
	initializer@ ObTypeInitializer_ParseFunction + @ type@ ObType_ParseFunction + !
	initializer@ ObTypeInitializer_SetSecurityFunction + @ type@ ObType_SetSecurityFunction + !

	initializer@ ObTypeInitializer_WaitOffset + @ type@ ObType_WaitOffset + !

	0 type@ ObType_ObjectListHead + !

	if (typetype@ ~~)
		type@ ObTypeObjectType!
		type@ ObHeader_SIZEOF - type@ ObType_ObjectListHead + !

		auto hdr
		type@ ObHeader_SIZEOF - hdr!

		type@ hdr@ ObHeader_TypeObject + !
	end elseif (ObObjectTypesDirectoryObject@)
		type@ ObHeader_SIZEOF - // object
		ObObjectTypesDirectoryObject@ // directory
		ObDirectoryInsertByPointers ok!

		if (ok@)
			ok@ type@ ObHeader_SIZEOF - ObHeader_Name + @ "ObTypeCreate: couldn't insert type '%s' into /ObjectTypes (%i)\n" KeCrash
		end
	end
end