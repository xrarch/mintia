//
// Implements HAL argument checking.
//

#include "<df>/dragonfruit.h"
#include "../../OSLoader/OSLoaderGlobal.h"

var HALArgsBuffer 0

buffer HALLocalArgsBuffer 256

fn HALArgsInit { ldrinfo -- }
	fnsection "INIT$text"

	HALLocalArgsBuffer // dest
	ldrinfo@ LdrInfo_ArgsBuffer + @ // src
	256 // size
	memcpy

	HALLocalArgsBuffer HALArgsBuffer!
end

buffer HALArgsNameBuffer 32

fn HALArgsValue { arg -- out }
	auto p
	HALArgsBuffer@ p!

	auto tp
	HALArgsNameBuffer tp!

	auto tpm
	tp@ 32 + tpm!

	auto m
	p@ 255 + m!

	0 out!

	while (p@ m@ <)
		if (p@ gb ~~)
			HALArgsNameBuffer tp!

			1 p +=

			if (p@ gb ~~)
				break
			end
		end elseif (p@ gb '=' ==)
			0 tp@ sb

			if (HALArgsNameBuffer arg@ strcmp)
				p@ 1 + out!
				return
			end

			p@ strlen p +=

			continue
		end

		p@ gb tp@ sb

		1 tp +=
		1 p +=
	end
end

fn HALArgsCheck { arg -- present }
	auto p
	HALArgsBuffer@ p!

	auto m
	p@ 255 + m!

	0 present!

	while (p@ m@ <)
		if (p@ gb ~~)
			break
		end

		if (arg@ p@ strcmp)
			1 present!
			return
		end else
			p@ strlen 1 + p +=
		end
	end
end