//
// Implements a hack to read a string from the non-video HAL console.
//

#include "<df>/dragonfruit.h"
#include "<inc>/HALConsole.h"

#include "<inc>/DebugIO.h"

fn DebugGets { s max -- }
	auto len
	0 len!

	while (1)
		auto c
		ERR c!
		while (c@ ERR ==)
			HALGetc c!
		end

		if (c@ '\r' ==)
			'\n' HALPutc
			break
		end

		if (c@ '\b' ==)
			if (len@ 0 >)
				1 len -=
				0 s@ len@ + sb
				'\b' HALPutc
				' ' HALPutc
				'\b' HALPutc
			end
		end elseif (len@ max@ <)
			c@ s@ len@ + sb

			1 len +=
			c@ HALPutc
		end
	end

	0 s@ len@ + sb
end